plugins {
    id "java"
    id "maven-publish"
}

group "org.moddingx"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
	mavenCentral()
	maven { url "https://maven.moddingx.org" }
}

dependencies {
	implementation "org.moddingx:CurseWrapper:3.0"

	implementation 'org.jsoup:jsoup:1.15.2'
	implementation "net.sf.jopt-simple:jopt-simple:6.0-alpha-3"
}

jar {
	manifest {
		attributes([
		        "Main-Class": "org.moddingx.modlistcreator.Main"
		])
	}
}

task fatjar(type: Jar) {
	archiveClassifier = "fatjar"
	manifest = jar.manifest
	with jar
}
build.dependsOn fatjar

task configureFatJar {
	doLast {
		configurations.runtimeClasspath.each { dep ->
			fatjar.from(project.zipTree(dep)) {
				exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
	outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url '../moddingx/maven'
        }
    }
}
